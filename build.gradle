/*
 * Copyright 2016 Jithu Menon
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.4'
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.bmuschko.tomcat'

sourceCompatibility = 1.8
version = '1.0'

repositories {
    mavenLocal()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots"
    }

    mavenCentral()

}

configurations.all {
    resolutionStrategy {
        // Need this to override guice-servlet:3.0 pulled in via jersey-guice
        force "com.google.inject.extensions:guice-servlet:4.0"
    }
}

dependencies {
    compile "com.sun.jersey.contribs:jersey-guice:1.19"
    compile "com.netflix.karyon:karyon3-core:3.0.1-rc.20"
    compile "com.netflix.karyon:karyon3-servlet:3.0.1-rc.20"
    compile "com.google.inject.extensions:guice-servlet:4.0"
    compile "org.slf4j:slf4j-log4j12:1.7.13"
    compile "io.swagger:swagger-jersey-jaxrs:1.5.7"
    providedCompile "javax.servlet:servlet-api:2.5"

    testCompile "org.testng:testng:6.9.10"

    def tomcatVersion = '7.0.59'
    tomcat  "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}

tomcat {
    httpPort = 9090
    contextPath = "/karyon"
}

task installSwaggerUiViewer(type: Exec) {
    commandLine 'sh', './makeSwaggerUiViewer.sh'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}